import com.satergo.build.SatergoBuildPlugin

plugins {
	id 'java'
	id 'application'
	id 'com.github.johnrengelman.shadow' version '8.1.1'
}

apply plugin: SatergoBuildPlugin

java {
	sourceCompatibility = JavaLanguageVersion.of(20)
	toolchain {
		languageVersion = JavaLanguageVersion.of(20)
	}
}

group 'com.satergo'
version '1.5.1'

application {
	mainClass = "com.satergo.Launcher"
	applicationName = project.name
}

repositories {
	mavenCentral()
	maven { url 'https://oss.sonatype.org/content/repositories/snapshots/' }
}

boolean buildInstaller = project.hasProperty("buildInstaller")
boolean platformProvided = project.hasProperty("platform")
String platform
if (platformProvided && project.findProperty("platform") != "auto") {
	// for releasing
	platform = project.findProperty("platform")
} else {
	// for running
	def currentOs = org.gradle.internal.os.OperatingSystem.current()
	if (currentOs.isLinux()) platform = "linux"
	else if (currentOs.isMacOsX()) platform = "mac"
	else if (currentOs.isWindows()) platform = "win"
	else throw new IllegalArgumentException("unknown operating system")

	def arch = System.getProperty("os.arch")
	if (arch != "amd64") platform += "-" + arch
}

def javaFxVersion = '20.0.2'
def javaFxModules = [ 'base', 'controls', 'fxml', 'graphics' ]

dependencies {
	implementation ('com.pixelduke:FXSkins:1.0.0') {
		exclude group: 'org.openjfx'
		exclude group: 'org.controlsfx'
	}
	implementation 'org.fxmisc.flowless:flowless:0.7.0'

	implementation 'org.ergoplatform:ergo-appkit_2.12:5.0.1'
	implementation 'com.typesafe:config:1.4.2'
	implementation 'org.slf4j:slf4j-simple:1.7.36'
	implementation 'com.grack:nanojson:1.8' // used for numerous HTTP APIs and ProgramData saving
	implementation 'com.google.code.gson:gson:2.10.1'
	implementation 'info.debatty:java-string-similarity:2.0.0' // used for mnemonic phrase words
	implementation 'com.google.zxing:core:3.5.1' // used for qr codes
	javaFxModules.forEach({ md -> implementation "org.openjfx:javafx-$md:$javaFxVersion:$platform" })
}

jar {
	manifest {
		attributes("Specification-Version": project.version)
		attributes(
				"Package-Type": buildInstaller ? "INSTALLATION" : "PORTABLE",
				"Package-Platform": platform,
				/* Section name */ "Satergo")
	}
}

satergoRuntime {

	extraModules = ["jdk.crypto.ec", "java.naming", "jdk.charsets"]
	extraJlinkOptions = ["--strip-debug", "--compress", "2", "--no-header-files", "--no-man-pages"]
	cleanupRuntimeContent = true
	cacheRuntimes = true

	proguardConfig = file("proguard.pro").toPath()
	proguardOutputName = "Satergo-${project.version}-${platform}-shrunk.jar"

	includeJarInRuntime = !buildInstaller

	launcherScript {
		type = platform == "win" ? "bat" : "sh"
		name = "Satergo"
		mainClass = application.mainClass.get()
		defaultJvmOpts = ['-Dsatergo.launcher={APP_HOME}/bin/Satergo',
						  '-Dsatergo.platform=' + platform ]
		windowsConsole = false
	}
	if (buildInstaller)
		launcherScript = null

	switch (platform) {
		case "linux": {
			platformName = "linux-x64"
			jdkRuntimeURI = URI.create("https://download.java.net/java/GA/jdk20.0.2/6e380f22cbe7469fa75fb448bd903d8e/9/GPL/openjdk-20.0.2_linux-x64_bin.tar.gz")
			break
		}
		case "linux-aarch64": {
			platformName = "linux-aarch64"
			jdkRuntimeURI = URI.create("https://download.java.net/java/GA/jdk20.0.2/6e380f22cbe7469fa75fb448bd903d8e/9/GPL/openjdk-20.0.2_linux-aarch64_bin.tar.gz")
			break
		}
		case "win": {
			platformName = "windows-x64"
			jdkRuntimeURI = URI.create("https://download.java.net/java/GA/jdk20.0.2/6e380f22cbe7469fa75fb448bd903d8e/9/GPL/openjdk-20.0.2_windows-x64_bin.zip")
			break
		}
		case "mac": {
			platformName = "mac-x64"
			jdkRuntimeURI = URI.create("https://download.java.net/java/GA/jdk20.0.2/6e380f22cbe7469fa75fb448bd903d8e/9/GPL/openjdk-20.0.2_macos-x64_bin.tar.gz")
			jdkRuntimeRoot = "Contents/Home/"
			break
		}
		case "mac-aarch64": {
			platformName = "mac-aarch64"
			jdkRuntimeURI = URI.create("https://download.java.net/java/GA/jdk20.0.2/6e380f22cbe7469fa75fb448bd903d8e/9/GPL/openjdk-20.0.2_macos-aarch64_bin.tar.gz")
			jdkRuntimeRoot = "Contents/Home/"
			break
		}
		default: throw new IllegalArgumentException()
	}

	createArchive = !buildInstaller
	archiveOutputPath = file("runtimes/${project.name}-v${project.version}-${platformName}.zip").toPath()

	// Create a convenient run file in the top level
	doBeforeArchival {
		if (platform == "win") {
			def runFile = file("${buildDir}/${runtimeDirectoryName}/run.bat")
			runFile.write('@start /min "" "%~dp0\\bin\\' + project.name + '.bat" %*')
		} else {
			def runFile = file("${buildDir}/${runtimeDirectoryName}/run.${platform.startsWith("mac") ? "command" : "sh"}")
			runFile.write('#!/bin/sh\n' +
					'eval "\\"$(dirname "$0")/bin/' + project.name + '\\" $*"')
			runFile.setExecutable(true)
		}
	}
}

satergoWinInstaller {
	def packagingDir = file("src/main/packaging").toPath()

	installerType = "MSI"

	appName = "Satergo"
	appVersion = project.version.toString()
	icoIconPath = packagingDir.resolve("windows-icon.ico")
	vendor = "Satergo"
	description = "The desktop wallet for the Ergo cryptocurrency"

	aboutUrl = "https://satergo.com"
	updateUrl = "https://satergo.com"

	windowsUpgradeUUID = "ef3fc1f4-414b-43e6-a2c2-cb4f7ccab4de"

	fileAssociations = [ packagingDir.resolve("erg-file-association.properties") ]

	extraArgs = [ "--resource-dir", packagingDir.resolve("windows-resources").toAbsolutePath().toString() ]

	outputDir = file("installers").toPath()

}

tasks.satergoWinInstaller {
	doFirst {
		if (!buildInstaller) throw new RuntimeException("Specify -PbuildInstaller to build an installer")
	}
}

tasks.withType(JavaCompile).configureEach {
	options.encoding = 'UTF-8'
}

// Make builds reproducible
tasks.withType(AbstractArchiveTask).configureEach {
	preserveFileTimestamps = false
	reproducibleFileOrder = true
}